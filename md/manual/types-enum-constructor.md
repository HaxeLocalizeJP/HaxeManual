## 2.4.1 列挙型のコンストラクタ

クラスと同じように、列挙型もそのコンストラクタを使うことでインスタンス化を行います。しかし、クラスとは異なり列挙型は複数のコンストラクタを持ち、以下のようにそのコンストラクタの名前を使って呼び出します。

```haxe
var a = Red;
var b = Green;
var c = Rgb(255, 255, 0);
```
このコードでは変数`a`、`b`、`c`の型は`Color`です。変数`c`は`Rgb`コンストラクタと引数を使って初期化されています。

すべての列挙型のインスタンスは`EnumValue`という特別な型に対して代入が可能です。

> ##### Define: EnumValue
>
> EnumValueはすべての列挙型のインスタンスと一致する特別な型です。この型はHaxeの標準ライブラリでは、すべての列挙型に対して可能な操作を提供するのに使われます。またユーザーのコードでは、特定の列挙型ではなく任意の列挙型のインスタンスを要求するAPIで利用できます。

以下の例からわかるように、列挙型とそのインスタンスを区別することは大切です。

```haxe
enum Color {
  Red;
  Green;
  Blue;
  Rgb(r:Int, g:Int, b:Int);
}

class Main {
  static public function main() {
    var ec:EnumValue = Red; // valid
    var en:Enum<Color> = Color; // valid
    // Error: Color should be Enum<Color>
    //var x:Enum<Color> = Red;
  }
}

```

もし、上でコメント化されている行のコメント化が解除された場合、このコードはコンパイルできなくなります。これは、列挙型のインスタンスである`Red`は、列挙型である`Enum<Color>`型の変数には代入できなくなるためです。

この関係性は、クラスとそのインスタンスの関係性に似ています。

> ##### Trivia: `Enum<T>の型パラメータを具体化する`
>
> このマニュアルのレビューアの一人は上のサンプルコードの`Color`と`Enum<Color>`の違いについて困惑しました。実際、型パラメータの具体化は意味のないもので、デモンストレーションのためのものでしかありませんでした。私たちはよく型を書くのを省いて、型についてあつかうのを[型推論](type-system-type-inference.md)にまかせてしまいます。
> 
> しかし、型推論では`Enum<Color>`ではないものが推論されるでしょう。コンパイラは、列挙型のコンストラクタをフィールドとしてみなした、仮の型を推論します。現在のHaxe3.2.0では、この仮の型について表現することは不可能であり、また表現する必要もありません。

---

Previous section: [列挙型インスタンス](types-enum-instance.md)

Next section: [列挙型を使う](types-enum-using.md)